apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    defaultConfig {
        applicationId "com.chyrta.converter"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.releaseVersionCode
        versionName Versions.releaseVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation ProjectDependencies.kotlin
    implementation ProjectDependencies.supportAppCompat
    implementation ProjectDependencies.supportRecyclerView
    implementation ProjectDependencies.supportDesign
    implementation ProjectDependencies.currencyEditText
    implementation ProjectDependencies.rxjava
    implementation ProjectDependencies.rxandroid
    implementation ProjectDependencies.retrofit
    implementation ProjectDependencies.retrofitAdapter
    implementation ProjectDependencies.retrofitGson
    implementation ProjectDependencies.koin
    implementation ProjectDependencies.koinViewModels
    implementation ProjectDependencies.lifecycle

    testImplementation ProjectDependencies.mockitoKotlin
    testImplementation ProjectDependencies.junit
    testImplementation ProjectDependencies.mockito
    testImplementation ProjectDependencies.hamcrest

    androidTestImplementation ProjectDependencies.androidTestRunner
    androidTestImplementation ProjectDependencies.espresso

    ktlint ProjectDependencies.ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--reporter=plain",
            "--reporter=checkstyle,output=${buildDir}/reports/ktlint/checkstyle.xml",
            "${projectDir}/**/src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "${projectDir}/**/src/**/*.kt"
}